#define TILE_DIM 32
#define BLOCK_ROWS 8

__global__ void transpose_pad(float *odata, const float *idata,
                              int width, int height) {
  __shared__ float tile[TILE_DIM][TILE_DIM + 1]; // +1 to break bank-aliasing

  int x = blockIdx.x * TILE_DIM + threadIdx.x;
  int y = blockIdx.y * TILE_DIM + threadIdx.y;

  // Load tile from global memory into shared (coalesced).
  for (int j = 0; j < TILE_DIM; j += BLOCK_ROWS) {
    if (x < width && (y + j) < height) {
      tile[threadIdx.y + j][threadIdx.x] = idata[(y + j) * width + x];
    }
  }
  __syncthreads();

  // Transpose tile and write back to global memory.
  x = blockIdx.y * TILE_DIM + threadIdx.x; // transpose block coordinates
  y = blockIdx.x * TILE_DIM + threadIdx.y;

  for (int j = 0; j < TILE_DIM; j += BLOCK_ROWS) {
    if (x < height && (y + j) < width) {
      odata[(y + j) * height + x] = tile[threadIdx.x][threadIdx.y + j];
    }
  }
}